import org.jsonschema2pojo.SourceType

plugins {
    id 'com.google.protobuf' version '0.9.4'
    id 'com.github.eirnym.js2p' version '1.0'
}

dependencies {
    implementation 'javax.validation:validation-api:1.1.0.Final'
    implementation 'org.apache.commons:commons-lang3:3.11'

    implementation 'com.google.protobuf:protobuf-java:3.25.1'
}

jsonSchema2Pojo {
    sourceType = SourceType.YAMLSCHEMA
    source = files("${sourceSets.main.output.resourcesDir}/airbyte_protocol")
    targetDirectory = new File(project.buildDir, 'generated/src/gen/java/')
    removeOldOutput = true

    targetPackage = 'io.airbyte.protocol.models'

    useLongIntegers = true
    generateBuilders = true
    includeConstructors = false
    includeSetters = true
}

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)

task generatePythonProtocolClassFiles(type: Exec) {
    inputs.dir 'src/main/resources/airbyte_protocol'
    inputs.dir 'bin'
    outputs.dir 'python/airbyte_protocol/models'

    commandLine 'bin/generate-python-classes-docker.sh'
}

task generateTypescriptProtocolClassFiles(type: Exec) {
    inputs.dir 'src/main/resources/airbyte_protocol'
    inputs.dir 'bin'
    outputs.dir 'typescript/dist'

    commandLine 'bin/generate-typescript-classes-docker.sh'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.1' // Use the appropriate version
    }

    generateProtoTasks {
        ofSourceSet('main')
    }
}
